{
  description = "The built-in styles of clang-format.";

  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs/nixos-unstable";
    utils.url = "github:numtide/flake-utils";
  };

  outputs = {
    self,
    nixpkgs,
    utils,
  }:
    utils.lib.eachDefaultSystem (system: let
      pkgs = nixpkgs.legacyPackages.${system};

      llvmPackages = pkgs.llvmPackages_14;
      clang-tools = pkgs.clang-tools.override {inherit llvmPackages;};
    in {
      packages.default = pkgs.stdenv.mkDerivation {
        pname = "clang-format-styles";
        version = clang-tools.version;

        dontUnpack = true;
        buildInputs = [clang-tools];

        installPhase = ''
          STYLES=(LLVM GNU Google Chromium Microsoft Mozilla WebKit)
          HEADER="### generated by clang-format ${clang-tools.version}"

          mkdir -p "$out"

          for style in "''${STYLES[@]}"; do
            style_file="$out/clang-format-''${style@L}.yml"

            echo "$HEADER" > "$style_file"
            clang-format --style="$style" --dump-config >> "$style_file"
          done
        '';
      };
    });
}
